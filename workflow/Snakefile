# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.

import os, shutil, yaml
from pathlib import Path
import pandas as pd
from snakemake.utils import min_version
from snakemake.utils import validate

##### set minimum snakemake version #####
min_version("7.16.0")


# define configfile, validate
configfile: "config/config.yaml"


validate(config, "schemas/config.schema.yaml")


# load runsheet, validate
runs = pd.read_csv(config["runsheet"], sep="\t")
validate(runs, "schemas/runsheet.schema.yaml")
runs.set_index("run_id", inplace=True, drop=False)

# load patientsheet, validate
patients = pd.read_csv(config["patientsheet"], sep="\t")
validate(patients, "schemas/patientsheet.schema.yaml")
patients.set_index("patient_id", inplace=True, drop=False)

# copy config data to output directory
Path(config["outdir"]).mkdir(exist_ok=True)
shutil.copy("config/config.yaml", config["outdir"])
shutil.copy(config["runsheet"], config["outdir"])
shutil.copy(config["patientsheet"], config["outdir"])

# indicator variable for if this run is a test or not
config["istest"] = True if "ngs-test-data" in runs.r1[0] else False


# include rules in other files
include: "rules/ref.smk"
include: "rules/map_count.smk"


if config["use_Demuxlet"]:

    include: "rules/demuxlet.smk"


include: "rules/preprocess.smk"
include: "rules/compass.smk"


# remove Veloctyo from soloFeatures to prevent expansion
if "Velocyto" in config["STARsolo"]["soloFeatures"]:
    config["STARsolo"]["soloFeatures"].remove("Velocyto")


rule all:
    input:
        expand(
            rules.render_STARsolo_report.output,
            soloFeatures=config["STARsolo"]["soloFeatures"],
            outdir=config["outdir"],
        ),
        expand(
            rules.render_filter_report.output,
            soloFeatures=config["STARsolo"]["soloFeatures"],
            outdir=config["outdir"],
        ),
        expand(
            rules.render_integrate_report.output,
            soloFeatures=config["STARsolo"]["soloFeatures"],
            outdir=config["outdir"],
        ),
        expand(
            rules.render_cluster_report.output,
            soloFeatures=config["STARsolo"]["soloFeatures"],
            outdir=config["outdir"],
        ),
